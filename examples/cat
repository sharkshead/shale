#!/usr/local/bin/shale

// Here's a very simple use of the file library to implement something like cat.

// Either takes a replacement of f=\"...file...\"
// for exampe: ./cat f=\"/etc/hosts\"
// or with no replacement will read the file name from stdin.



// Load the file library. See
//  help file::()
// for the functionality provided by this library.
file library

// This if leaves the file name on the stack or exits if no file name is entered.
f initialised {
  f
} {
  "File: " print
  stdin file:: fgets file::() not {
    "no file given" println
    1 exit
  } ifthen
} if

// Define the cat function, which assumes the file name is on the top of the stack.
cat var
cat {
  0$ var

  "r" open file::() {         // Attempt to open the file read-only.
    0$ swap =                 // Grab the file handle.
    { 0$ fgets file::() } {   // Iterate through each line until we hit EOF.
      println
    } while
    0$ close file::()
  } {
    "Cannot open file" println
  } if
} =

// Execute the cat function.
cat()
