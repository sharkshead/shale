#!/usr/local/bin/shale

// This solves a classic sudoku with standard row/column/box constraints, optionally with an anti-Knight constraint.
// It is based on the python code from the Computerphile video https://www.youtube.com/watch?v=G_UYXzGuqvM
// The sudoku example from this video is used below, along with a couple of Cracking The Cryptic examples.

// The sudoku grid is stored in a multi-dimensional array under the grid:: namespace.
// The row and column of each cell is represented by:
//
//  row column:: grid::
//
// A 0 value represents an empty cell, and a 1 to 9 value represents a cell value.

// Increment a variable. Expects a variable name on the stack.
increment var
increment {
  dup 1 + =
} =

// Prnts the sudoku grid.
printGrid var
printGrid {
  r var
  c var

  r 0 =
  { r 9 < } {
    c 0 =
    { c 9 < } {
      r.value c.value:: grid:: dup 0 == { pop " ." } { " %d" } if printf
      c increment()
    } while
    "" println
    r increment()
  } while
} =

// Assign the cell values to one row of the grid.
setRow var
setRow {
  8$ var 8$ swap =    // cell 9 of the row
  7$ var 7$ swap =    // cell 8 of the row
  6$ var 6$ swap =    // ...
  5$ var 5$ swap =
  4$ var 4$ swap =
  3$ var 3$ swap =
  2$ var 2$ swap =
  1$ var 1$ swap =    // ...
  0$ var 0$ swap =    // cell 1 of the row
  r var r swap =      // the row number
  i var               // loop counter

  r 0 < r 8 > or {
    r "Illegal row %d\n" printf
    1 exit
  } ifthen

  i 0 =
  { i 9 < } {
    i.value$ 0 < i.value$ 9 > or {
      i r i.value$ "Illegal value %d specified for r%dc%d\n" printf
      1 exit
    } ifthen
    r.value i.value:: grid:: defined not {    // define this grid cell if not already defined
      r.value i.value:: grid:: var
    } ifthen
    r.value i.value:: grid:: i.value$ =       // assign the value to the grid cell
    i increment()
  } while
} =

// Is it possible to place a digit in a given cell?
possible var
possible {
  n var n swap =
  c var c swap =
  r var r swap =
  i var
  j var
  r0 var
  c0 var
  ok var

  ok true =

  // Check the column doesn't already contain this value.
  i 0 =
  { i 9 < } {
    r.value i.value:: grid:: n == {
      ok false =
      break
    } ifthen
    i increment()
  } while

  // Check the row doesn't already contain this value.
  ok {
    i 0 =
    { i 9 < } {
      i.value c.value:: grid:: n == {
        ok false =
        break
      } ifthen
      i increment()
    } while

    // Check that the box doesn't already contain this value.
    ok {
      r0 r 3 / 3 * =
      c0 c 3 / 3 * =
      i 0 =
      { i 3 < } {
        j 0 =
        { j 3 < } {
          r0 i + c0 j +:: grid:: n == {
            ok false =
            break
          } ifthen
          j increment()
        } while
        ok not { break } ifthen
        i increment()
      } while

      // Check for any other constraint.
      ok { constraint initialised { r c n constraint() not } and } and {
        ok false =
      } ifthen
    } ifthen
  } ifthen

  ok.value
} =

// This is an anti-Knight constraint.
antiKnightConstraint var
antiKnightConstraint {
  0$ var 0$ swap =  // n
  1$ var 1$ swap =  // column
  2$ var 2$ swap =  // row
  nr var
  nc var
  ok var

  ok true =

  // Check Knight's move 2 left and 1 up and down.
  nc 1$ 2 - =
  nc 0 >= {
    nr 2$ 1 - =
    nr 0 >= {
      nr.value nc.value:: grid:: 0$ == {
        ok false =
      } ifthen
    } ifthen
    ok {
      nr 2$ 1 + =
      nr 9 < {
        nr.value nc.value:: grid:: 0$ == {
          ok false =
        } ifthen
      } ifthen
    } ifthen
  } ifthen

  // Check Knight's move 1 left and 2 up and down.
  ok {
    nc 1$ 1 - =
    nc 0 >= {
      nr 2$ 2 - =
      nr 0 >= {
        nr.value nc.value:: grid:: 0$ == {
          ok false =
        } ifthen
      } ifthen
      ok {
        nr 2$ 2 + =
        nr 9 < {
          nr.value nc.value:: grid:: 0$ == {
            ok false =
          } ifthen
        } ifthen
      } ifthen
    } ifthen
  } ifthen

  // Check Knight's move 1 right and 2 up and down.
  ok {
    nc 1$ 1 + =
    nc 9 < {
      nr 2$ 2 - =
      nr 0 >= {
        nr.value nc.value:: grid:: 0$ == {
          ok false =
        } ifthen
      } ifthen
      ok {
        nr 2$ 2 + =
        nr 9 < {
          nr.value nc.value:: grid:: 0$ == {
            ok false =
          } ifthen
        } ifthen
      } ifthen
    } ifthen
  } ifthen

  // Check Knight's move 2 right and 1 up and down.
  ok {
    nc 1$ 2 + =
    nc 9 < {
      nr 2$ 1 - =
      nr 0 >= {
        nr.value nc.value:: grid:: 0$ == {
          ok false =
        } ifthen
      } ifthen
      ok {
        nr 2$ 1 + =
        nr 9 < {
          nr.value nc.value:: grid:: 0$ == {
            ok false =
          } ifthen
        } ifthen
      } ifthen
    } ifthen
  } ifthen

  ok.value
} =

// Leave this undefined to get the standard sudoku rules, or set this
// to constraint code such as antiKnightContraint defined above.
constraint var

solve var
solve {
  r var
  c var
  n var
  prt var

  prt true =
  r 0 =
  { r 9 < } {
    c 0 =
    { c 9 < } {
      r.value c.value:: grid:: 0 == {
        n 1 =
        { n 10 < } {
          r.value c.value n.value possible() {
            r.value c.value:: grid:: n =
            solve()
            r.value c.value:: grid:: 0 =
          } ifthen
          n increment()
        } while
        prt false =
        break
      } ifthen
      c increment()
    } while
    prt not { break } ifthen
    r increment()
  } while

  prt {
    "" println
    printGrid()
  } ifthen
} =

// As mentioned above, this is the example taken from the Computerphile video.
// Raspberry Pi3 Model A time: 22 seconds
//
0 5 3 0 0 7 0 0 0 0 setRow()
1 6 0 0 1 9 5 0 0 0 setRow()
2 0 9 8 0 0 0 0 6 0 setRow()
3 8 0 0 0 6 0 0 0 3 setRow()
4 4 0 0 8 0 3 0 0 1 setRow()
5 7 0 0 0 2 0 0 0 6 setRow()
6 0 6 0 0 0 0 2 8 0 setRow()
7 0 0 0 4 1 9 0 0 5 setRow()
8 0 0 0 0 8 0 0 7 9 setRow()

// From https://www.youtube.com/watch?v=MXUgYxHmKq4&t=0s
// This sudoku was featured on Cracking The Cryptic, and takes considerably longer than the one above.
// Raspberry Pi3 Model A time: 18 minutes total elapsed time, although the
// solution only takes a couple of minutes to be printed. The rest of the time is spent
// looking for other, non-existant, solutions.
//
// 0 0 6 8 0 0 0 0 1 3 setRow()
// 1 0 0 0 9 0 1 0 0 0 setRow()
// 2 0 0 0 0 0 8 0 0 4 setRow()
// 3 0 1 0 0 4 0 5 0 0 setRow()
// 4 0 3 0 0 0 9 0 0 0 setRow()
// 5 0 8 5 0 0 0 0 7 0 setRow()
// 6 0 2 0 0 0 7 3 0 0 setRow()
// 7 0 0 0 0 9 4 0 0 6 setRow()
// 8 4 0 0 0 6 0 0 0 0 setRow()

// From https://www.youtube.com/watch?v=rQHV-gIAG_0
// Another Cracking The Cryptic video, this one with an anti-Knight move constraint.
// Raspberry Pi3 Model A time: 54 seconds to the solution, and almost 7 minutes
// to complete.
//
// constraint antiKnightConstraint =
// 0 0 0 0 0 0 6 0 0 0 setRow()
// 1 0 0 3 0 0 0 0 0 7 setRow()
// 2 2 0 0 3 0 0 4 9 0 setRow()
// 3 6 0 0 0 0 0 0 4 5 setRow()
// 4 0 0 2 0 0 0 8 0 0 setRow()
// 5 0 0 0 1 0 0 0 0 0 setRow()
// 6 3 0 0 0 0 0 0 0 0 setRow()
// 7 7 0 0 0 0 1 0 0 9 setRow()
// 8 0 0 0 0 0 0 5 0 0 setRow()

// Another CTC sudoku: https://www.youtube.com/watch?v=vH-JooV8RA4&t=0s
// On a Pi3 Model A it takes a couple of minutes to produce the completed grid
// but takes an hour to finish.
// 0 0 0 0 0 0 0 0 0 0 setRow()
// 1 0 0 9 8 0 0 0 0 7 setRow()
// 2 0 8 0 0 6 0 0 5 0 setRow()
// 3 0 5 0 0 4 0 0 3 0 setRow()
// 4 0 0 7 9 0 0 0 0 2 setRow()
// 5 0 0 0 0 0 0 0 0 0 setRow()
// 6 0 0 2 7 0 0 0 0 9 setRow()
// 7 0 4 0 0 5 0 0 6 0 setRow()
// 8 3 0 0 0 0 6 2 0 0 setRow()

"Initial grid" println
printGrid()

solve()
